//PONY FALLOUT METER
integer hunger;
integer thirst;
string super_edgy_unique_identifier = "â˜¢ ";

list meter_info = 
[
    "Your name goes here.",
    "Your sex goes here.",
    "Your race goes here.",
    "Hunger",
    "Thirst"
];

integer M_NAME = 0;
integer M_SEX = 1;
integer M_RACE = 2;
integer M_HUNGER = 3;
integer M_THIRST = 4;

list hash()
{
    integer rand = llRound(llFrand(99999)) + 99999;
    
    while(~llListFindList(recent_randout,[rand]))
    rand = llRound(llFrand(99999)) + 99999;
    
    recent_randout += [rand,45];
    
    integer unix = llGetUnixTime();
    string keyhash = llMD5String( (string)llGetOwner() + (string)rand + (string)unix,rand+unix+1337);
    return [keyhash,rand,unix];
}

encode_message(string target_id, list params_out)
{
    if(target_id == NULL_KEY)
    llRegionSay(chan(target_id),llDumpList2String(params_out + hash(),"|"));
    else
    llRegionSayTo(target_id,chan(llGetOwnerKey(target_id)),llDumpList2String(params_out + hash(),"|"));
}

list recent_randout;
list recent_hashtags;
hashprocess()
{
    integer x;
    for(x=0;x<llGetListLength(recent_hashtags);x+=2)
    {
        integer entry = llList2Integer(recent_hashtags,x+1);
        entry--;
        if(entry<0)
        {
            recent_hashtags = llDeleteSubList(recent_hashtags,x,x+1);
            x-=2;
        }
        else
        {
            recent_hashtags = llListReplaceList(recent_hashtags,[entry],x+1,x+1);
        }
    }
    for(x=0;x<llGetListLength(recent_randout);x+=2)
    {
        integer entry = llList2Integer(recent_randout,x+1);
        entry--;
        if(entry<0)
        {
            recent_randout = llDeleteSubList(recent_randout,x,x+1);
            x-=2;
        }
        else
        {
            recent_randout = llListReplaceList(recent_randout,[entry],x+1,x+1);
        }
    }
}

process_message(string m, string id)
{
    list p = llParseString2List(m,["|"],[]);
    string keyhash = llList2String(p,-3);
    //does the hash match
    if(keyhash == llMD5String( (string)llGetOwnerKey(id) + llList2String(p,-2) + llList2String(p,-1),llList2Integer(p,-2) + llList2Integer(p,-1) + 1337))
    {
        //was the message sent in the last 30 seconds
        if((llGetUnixTime() - llList2Integer(p,-1)) < 30)
        {
            //have we already received this exact message (we will never encode the same hash twice from an object, which means this only happens if people are spoofing messages)
            if(~llListFindList(recent_hashtags,[keyhash]))
            return;
            else recent_hashtags += [keyhash,45];//45 seconds to message deprecator to ensure no 30sec windows ever get hit perfect
        }
        else return;
    }
    else return;
    
    //ok so now that we've filtered out the bullshit, let's do some stuff
    integer x;
    while(x < (llGetListLength(p) - 3))
    {
        string cmd = llList2String(p,x);
        /////////////////////////////////////////
        //Meter Name
        /////////////////////////////////////////
        if(cmd=="SetMeterName")
        {
            encode_message(llGetOwner(),["DialogCallback","MeterNameEntry","Please enter your displayed name.","!INPUTBOX"]);
            x++;
        }
        else if(cmd=="MeterNameEntry")
        {
            meter_info = llListReplaceList(meter_info,[llList2String(p,x+1)],M_NAME,M_NAME);
            x+=2;
        }
        /////////////////////////////////////////
        //Meter Sex
        /////////////////////////////////////////
        else if(cmd=="SetMeterSex")
        {
            encode_message(llGetOwner(),["DialogCallback","MeterSexEntry","Please enter your displayed gender.","!INPUTBOX"]);
            x++;
        }
        else if(cmd=="MeterSexEntry")
        {
            meter_info = llListReplaceList(meter_info,[llList2String(p,x+1)],M_SEX,M_SEX);
            x+=2;
        }
        /////////////////////////////////////////
        //Meter Race
        /////////////////////////////////////////
        else if(cmd=="SetMeterRace")
        {
            encode_message(llGetOwner(),["DialogCallback","MeterRaceEntry","Please enter your displayed race.","!INPUTBOX"]);
            x++;
        }
        else if(cmd=="MeterRaceEntry")
        {
            meter_info = llListReplaceList(meter_info,[llList2String(p,x+1)],M_RACE,M_RACE);
            x+=2;
        }
        /////////////////////////////////////////
        //Meter Hunger
        /////////////////////////////////////////
        else if(cmd=="SetMeterHunger")
        {
            integer h = llList2Integer(p,x+1);
            hunger=h;
            meter_info = llListReplaceList(meter_info,[(string)h + "/100"],M_HUNGER,M_HUNGER);
            x+=2;
        }
        /////////////////////////////////////////
        //Meter Thirst
        /////////////////////////////////////////
        else if(cmd=="SetMeterThirst")
        {
            integer h = llList2Integer(p,x+1);
            thirst=h;
            meter_info = llListReplaceList(meter_info,[llList2String(p,x+1) + "/100"],M_THIRST,M_THIRST);
            x+=2;
        }
        else x++;
    }
}

integer chan(string id)
{
    return (integer)("0x" + llGetSubString(id,0,4)) - 9000000;
}

integer listener;
newlisten()
{
    llListenRemove(listener);
    listener = llListen(chan(llGetOwner()),"","","");
}

default
{
    on_rez(integer r)
    {
        newlisten();
        encode_message(llGetOwner(),["MeterInfoRequest"]);
    }
    state_entry()
    {
        newlisten();
        llSetTimerEvent(1);
    }
    timer()
    {
        vector color = <0,1,0>;
        if(hunger<=25||thirst<=25)
        color = <1,0,0>;
        
        llSetText(super_edgy_unique_identifier + llDumpList2String(meter_info,"\n"),color,1);
        hashprocess();
    }
    listen(integer c, string n, key i, string m)
    {
        process_message(m,i);
    }
}